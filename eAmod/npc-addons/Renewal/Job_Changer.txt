// Reward Exchanges

-	shop	Renewal_Shop	-1,7931:1000,6124:2500,12341:10000,12734:1000

prontera,159,187,3	script	Gaia Girl	831,{
	mes "[^FFA500Gaia Girl^000000]";
	mes "Espero que estés disfrutando tu estadía en nuestros servidores de TerraOnline.";
	mes "Que puedo hacer por tí?";
	next;
	switch( select("Reset Skills:Reset Stats:Reset Both:Job Change Renewal:Renewal Shop") )
	{
	case 1: // Reset Skills
		mes "[^FFA500Gaia Girl^000000]";
		mes "El costo por el ^0000FFReset de Skills^000000 es de 50.000 zeny. Para poder realizarlo no debes superar 1000 de peso en tu inventario y no traer Pecopeco, Falcon o Carro.";
		next;
		if( select("Proceder con el reset","No gracias...") == 2 )
		{
			mes "[^FFA500Gaia Girl^000000]";
			mes "Vuelve cuando necesites algún servicio especial.";
			close;
		}

		if( Zeny < 50000 )
		{
			mes "[^FFA500Gaia Girl^000000]";
			mes "No traes suficiente Zeny contigo, lo lamento.";
			close;
		}

		if( Weight > 10000 )
		{
			mes "[^FFA500Gaia Girl^000000]";
			mes "Para resetear tus skills necesito que no excedas más de 1000 de peso de items contigo, así que deja algunas cosas en Storage y vuelve.";
			close;
		}

		if( checkriding() || checkfalcon() || checkcart() )
		{
			mes "[^FFA500Gaia Girl^000000]";
			mes "Reset de Skills requiere que vengas sin Carro/PecoPeco/Falcon. Quitalos y vuelve a hablarme.";
			close;
		}

		sc_end SC_ALL;
		resetskill;
		set Zeny, Zeny - 50000;

		mes "[^FFA500Gaia Girl^000000]";
		mes "Tus skills han sido reseteadas, piensa bien tus opciones. Suerte!!";
		close;
	case 2:
		mes "[^FFA500Gaia Girl^000000]";
		mes "El costo por el ^0000FFReset de Stats^000000 es de 50.000 zeny. Para poder realizarlo no debes superar 1000 de peso en tu inventario.";
		next;
		if( select("Proceder con el reset","No gracias...") == 2 )
		{
			mes "[^FFA500Gaia Girl^000000]";
			mes "Vuelve cuando necesites algún servicio especial.";
			close;
		}

		if( Zeny < 50000 )
		{
			mes "[^FFA500Gaia Girl^000000]";
			mes "No traes suficiente Zeny contigo, lo lamento.";
			close;
		}

		if( Weight > 10000 )
		{
			mes "[^FFA500Gaia Girl^000000]";
			mes "Para resetear tus stats necesito que no excedas más de 1000 de peso de items contigo, así que deja algunas cosas en Storage y vuelve.";
			close;
		}

		sc_end SC_ALL;
		resetstatus;
		set Zeny, Zeny - 50000;

		mes "[^FFA500Gaia Girl^000000]";
		mes "Tus stats han sido reseteadas, piensa bien tus opciones. Suerte!!";
		close;
	case 3:
		mes "[^FFA500Gaia Girl^000000]";
		mes "El costo por el ^0000FFReset de Skills y Stats^000000 es de 75.000 zeny. Para poder realizarlo no debes superar más de 1000 de peso y no traer Pecopeco, Falcon o Carro.";
		mes "Solamente te puedo hacer un reset total si no te has hecho reset de skills o stats en las ultimas 2 horas.";
		next;
		if( select("Proceder con el reset","No gracias...") == 2 )
		{
			mes "[^FFA500Gaia Girl^000000]";
			mes "Vuelve cuando necesites algún servicio especial.";
			close;
		}

		if( Zeny < 75000 )
		{
			mes "[^FFA500Gaia Girl^000000]";
			mes "No traes suficiente Zeny contigo, lo lamento.";
			close;
		}

		if( Weight > 10000 )
		{
			mes "[^FFA500Gaia Girl^000000]";
			mes "Para resetear tus skills y stats necesito que no excedas más de 1000 de peso de items contigo, así que deja algunas cosas en Storage y vuelve.";
			close;
		}

		if( checkriding() || checkfalcon() || checkcart() )
		{
			mes "[^FFA500Gaia Girl^000000]";
			mes "Reset de Skills requiere que vengas sin Carro/PecoPeco/Falcon. Quitalos y vuelve a hablarme.";
			close;
		}

		sc_end SC_ALL;
		resetskill;
		resetstatus;
		set Zeny, Zeny - 75000;

		mes "[^FFA500Gaia Girl^000000]";
		mes "Tus skills y stats han sido reseteadas, piensa bien tus opciones. Suerte!!";
		close;
	case 5: // Renewal Shop
		mes "[^FFA500Gaia Girl^000000]";
		mes "Cierra esta ventana para accesar a la Tienda Renewal, donde encontrarás materiales requeridos.";
		close2;
		callshop "Renewal_Shop",1;
		end;
	case 4: // Renewal Job Change
		if( checkriding() || checkfalcon() || checkcart() )
		{
			mes "[^FFA500Gaia Girl^000000]";
			mes "Para poder proceder con este servicio y ver si puedes renacer, debes quitarte el Pecopoco/Falco/Carro.";
			close;
		}
		if( SkillPoint != 0 )
		{
			mes "[^FFA500Gaia Girl^000000]";
			mes "Se necesita que uses todos tus skills points antes de realizar cambio de profesión.";
			mes "Vuelve pronto!!.";
			close;
		}
		if( BaseLevel < 99 )
		{
			mes "[^FFA500Gaia Girl^000000]";
			mes "Tu proximo cambio de profesión se podrá tramitar cuando seas nivel 99 base.";
			close;
		}

		set .@target_job, 0;
		if( Class >= Job_Knight && Class <= Job_Crusader2 )
		{ // Comming from Second Class
			if( JobLevel < 50 )
			{
				mes "[^FFA500Gaia Girl^000000]";
				mes "Tu proximo cambio de profesión se podrá tramitar cuando seas nivel 99/50.";
				close;
			}

			switch( Class )
			{
			case Job_Knight:		set .@target_job, Job_Rune_Knight; break;
			case Job_Priest:		set .@target_job, Job_Arch_Bishop; break;
			case Job_Wizard:		set .@target_job, Job_Warlock; break;
			case Job_Blacksmith:		set .@target_job, Job_Mechanic; break;
			case Job_Hunter:		set .@target_job, Job_Ranger; break;
			case Job_Assassin:		set .@target_job, Job_Guillotine_Cross; break;
			case Job_Crusader:		set .@target_job, Job_Royal_Guard; break;
			case Job_Monk:			set .@target_job, Job_Sura; break;
			case Job_Sage:			set .@target_job, Job_Sorcerer; break;
			case Job_Rogue:			set .@target_job, Job_Shadow_Chaser; break;
			case Job_Alchemist:		set .@target_job, Job_Genetic; break;
			case Job_Bard:			set .@target_job, Job_Minstrel; break;
			case Job_Dancer:		set .@target_job, Job_Wanderer; break;
			}
		}
		else if( Class >= Job_Lord_Knight && Class <= Job_Paladin2 )
		{ // Comming from Advanced
			if( JobLevel < 50 )
			{
				mes "[^FFA500Gaia Girl^000000]";
				mes "Tu proximo cambio de profesión se podrá tramitar cuando seas nivel 99/70.";
				close;
			}

			switch( Class )
			{
			case Job_Lord_Knight:		set .@target_job, Job_Rune_Knight_T; break;
			case Job_High_Priest:		set .@target_job, Job_Arch_Bishop_T; break;
			case Job_High_Wizard:		set .@target_job, Job_Warlock_T; break;
			case Job_Whitesmith:		set .@target_job, Job_Mechanic_T; break;
			case Job_Sniper:		set .@target_job, Job_Ranger_T; break;
			case Job_Assassin_Cross:	set .@target_job, Job_Guillotine_Cross_T; break;
			case Job_Paladin:		set .@target_job, Job_Royal_Guard_T; break;
			case Job_Champion:		set .@target_job, Job_Sura_T; break;
			case Job_Professor:		set .@target_job, Job_Sorcerer_T; break;
			case Job_Stalker:		set .@target_job, Job_Shadow_Chaser_T; break;
			case Job_Creator:		set .@target_job, Job_Genetic_T; break;
			case Job_Clown:			set .@target_job, Job_Minstrel_T; break;
			case Job_Gypsy:			set .@target_job, Job_Wanderer_T; break;
			}
		}
		else if( Class >= Job_Baby_Knight && Class <= Job_Baby_Crusader2 )
		{ // Comming from Baby Classes
			if( JobLevel < 50 )
			{
				mes "[^FFA500Gaia Girl^000000]";
				mes "Tu proximo cambio de profesión se podrá tramitar cuando seas nivel 99/50.";
				close;
			}

			switch( Class )
			{
			case Job_Baby_Knight:		set .@target_job, Job_Baby_Rune; break;
			case Job_Baby_Priest:		set .@target_job, Job_Baby_Bishop; break;
			case Job_Baby_Wizard:		set .@target_job, Job_Baby_Warlock; break;
			case Job_Baby_Blacksmith:	set .@target_job, Job_Baby_Mechanic; break;
			case Job_Baby_Hunter:		set .@target_job, Job_Baby_Ranger; break;
			case Job_Baby_Assassin:		set .@target_job, Job_Baby_Cross; break;
			case Job_Baby_Crusader:		set .@target_job, Job_Baby_Guard; break;
			case Job_Baby_Monk:		set .@target_job, Job_Baby_Sura; break;
			case Job_Baby_Sage:		set .@target_job, Job_Baby_Sorcerer; break;
			case Job_Baby_Rogue:		set .@target_job, Job_Baby_Chaser; break;
			case Job_Baby_Alchemist:	set .@target_job, Job_Baby_Genetic; break;
			case Job_Baby_Bard:		set .@target_job, Job_Baby_Minstrel; break;
			case Job_Baby_Dancer:		set .@target_job, Job_Baby_Wanderer; break;
			}
		}

		if( .@target_job == 0 )
		{
			mes "[^FFA500Gaia Girl^000000]";
			mes "Parece que tu clase no puede evolucionar a 3º Class.";
			close;
		}

		mes "[^FFA500Gaia Girl^000000]";
		mes "Tu personaje está preparado para evolucionar a 3º Class. Deseas proceder ahora?";
		next;
		if( select("Sí, cambiar a 3º Class:No, aún no haré el cambio") == 2 )
		{
			mes "[^FFA500Gaia Girl^000000]";
			mes "Vuelve cuando te hayas decidido finalmente.";
			close;
		}

		callfunc "Job_Change", .@target_job;
		mes "[^FFA500Gaia Girl^000000]";
		mes "Listo, ahora estás preparado para continuar tu progreso.";
		close;		
		
	}
}
